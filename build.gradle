plugins {
    id "idea"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
}

idea {
    project {
        languageLevel = 17
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "edu.sandbox"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:${Versions.springBootDependencies}")
            }

            dependency("org.springframework.shell:spring-shell-starter:${Versions.springBootShell}")
            dependency("org.projectlombok:lombok:${Versions.lombok}")
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()

            force 'org.scala-lang:scala-reflect:2.13.10'
            force 'org.scala-lang:scala-library:2.13.10'
        }
    }
}

task printManagedVersions {
    group "custom help"
    description "prints available dependencies"

    doLast {
        dependencyManagement.managedVersions.each {
            println it
        }
    }
}